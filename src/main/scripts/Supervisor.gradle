/*
 * Copyright (c) 2013 mgm technology partners GmbH
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
import java.text.DateFormat
import java.text.SimpleDateFormat

import org.apache.commons.io.FilenameUtils
import org.apache.commons.lang3.StringUtils
import org.apache.commons.lang3.time.StopWatch
import org.apache.tools.ant.taskdefs.condition.Os
import org.codehaus.plexus.util.cli.Commandline;

import com.mgmtp.perfload.supervisor.Commands
import com.mgmtp.perfload.supervisor.SupervisorTasks
import com.mgmtp.perfload.supervisor.SupervisorUtils

buildscript {
	dependencies {
		classpath fileTree(dir: 'lib')
	}
}

defaultTasks 'runLoadTest'

def config() {
	project.ext.commonConfig = SupervisorUtils.loadConfig(null, 'CommonConfig.groovy')

	if (commonConfig.multiTenancy) {
		if (!project.hasProperty('tenant')) {
			// add tenant to the script's binding, i. e. make it a global variable
			project.ext.tenant = System.getenv('PERFLOAD_TENANT')
		}
		if (!tenant) {
			throw new IllegalStateException("Multi-tenancy mode is activated but no tenant was specified.");
		}
	} else {
		project.ext.tenant = null
	}

	project.ext.supervisorConfig = SupervisorUtils.loadConfig(tenant, 'SupervisorConfig.groovy')
	project.ext.commands = SupervisorUtils.loadConfig(Commands.class)
	project.ext.loadTestConfig = SupervisorUtils.loadTestConfig(tenant, 'LoadTestConfig.groovy')
	project.ext.supervisorTasks = new SupervisorTasks(ant: ant, loadTestConfig: loadTestConfig, commands: commands)

	if (!supervisorConfig.requireTestComment) {
		project.ext.testComment = ''
	}
	if (supervisorConfig.runProjectSpecificTasks) {
		// includes project-specific targets as if they were directly defined in this file
		String projectTasksFile = "conf/${tenant ? tenant + '/' : ''}ProjectTasks.gradle"
		println "Including project targets file: $projectTasksFile"

		apply from: projectTasksFile
	}
}

def welcome() {
	int len = 65
	String copyright = StringUtils.center('(c) 2013, mgm technology partners GmbH', len)
	String welcomeMsg = StringUtils.center('Welcome to perfLoad\'s Supervisor ${project.version}', len)

	List list1 = []
	(len + 2).times { list1 << '*' }
	String msg1 = list1.join()

	List list2 = ['*']
	len.times { list2 << ' ' }
	list2 << '*'
	String msg2 = list2.join()

	String message = """
	$msg1
	$msg2
	$msg2
	*${welcomeMsg}*
	*${copyright}*
	$msg2
	$msg2
	$msg1
	"""

	println message

	println ("Running in " + (tenant ? 'multi-' : 'single-') + "tenancy mode.")
	if (tenant) {
		println ("Tenant: $tenant")
	}
}

task init(description: 'Initializes the Supervisor loading basic configuration for testplan-independent tasks') << {
	config()
	welcome()
}

task loadTestConfiguration(dependsOn: init, description: 'Loads the test plan possibly prompting for the testplan to be used.') << {
	// if not set via command-line (-Dtestplan=<...>), the user is prompted for the test config to be used
	if (!project.hasProperty('testplan')) {
		project.ext.testplan = promptForTestplan()
	}

	// Use the testplan file's base name as the test name and add it to the script's binding.
	// Thus, it is also accessible by an included 'ProjectTargets.gant' file.k
	project.ext.testname = FilenameUtils.getBaseName(testplan)

	// prefix the results directory with a timestamp
	DateFormat df = new SimpleDateFormat('yyyyMMdd-HHmm')

	def testResultsDir = new File(commonConfig.resultsDir, "${tenant ? tenant + '/' : ''}${df.format(new Date())}_${FilenameUtils.getBaseName(testplan)}")

	project.ext.testResultsDir = testResultsDir

	testResultsDir.mkdirs()
	supervisorTasks.resultsDir = testResultsDir

	// The number of daemons to launch and their ports are read from the testplan
	// and do not need any extra configuration.
	Map<String, List<String>> daemons = readDaemonsFromTestplan("${commonConfig.consoleDir}/testplans/$testplan")

	// add daemon ports to the config
	SupervisorUtils.enhanceConfigWithDaemonPorts(loadTestConfig, daemons)
}

task runLoadTest(dependsOn: loadTestConfiguration, description: 'Runs a load test.') << {
	// if not set via command-line (-DtestComment=<...>), the user is prompted for the test config to be used
	if (!project.hasProperty('testComment')) {
		def console = System.console()
		project.ext.testComment = console.readLine('\nPlease enter some comment for the test:')
	}

	def tasks = []

	if (supervisorConfig.cleanupBeforeTest) {
		tasks << cleanupFiles
	}

	if (supervisorConfig.runProjectSpecificTasks) {
		tasks << before
	}

	tasks << startPerfmons
	tasks << restartDaemons

	if (supervisorConfig.startServersBeforeTest) {
		tasks << restartServers
	}

	if (supervisorConfig.runProjectSpecificTasks) {
		tasks << performSystemCheck
	}

	tasks << startTest

	if (supervisorConfig.shutdownServersAfterTest) {
		tasks << stopServers
	}

	tasks << stopDaemons
	tasks << stopPerfmons

	if (supervisorConfig.runProjectSpecificTasks) {
		tasks << after
	}

	task 'writeTestComment' << {
		new File(commonConfig.consoleDir, 'perfload.meta.utf8.props').withWriterAppend('UTF-8') { writer ->
			new PrintWriter(writer).println("test.comment=${testComment}")
		}
	}
	tasks << writeTestComment

	if (supervisorConfig.collectResults) {
		tasks << zipFiles
		tasks << downloadFiles
	}

	if (supervisorConfig.createReport) {
		tasks <<  runPerfAlyzer
	}

	StopWatch stopWatch = new StopWatch()
	stopWatch.start()

	tasks.each {
		it.execute()
	}

	stopWatch.stop()

	println()
	println 'LOAD TEST EXECUTION FINISHED'
	println()
	println "Total execution time: $stopWatch"
}

task startTest(dependsOn: loadTestConfiguration, description: 'Starts a load test') << {
	SupervisorUtils.executeCommandLine("./console", commonConfig.consoleDir,
			[
				'-testplan',
				"testplans/$testplan",
				'-timeout',
				"${supervisorConfig.loadProfileTestTimeout}"
			])
}

task abortLoadTest(dependsOn: loadTestConfiguration, description: 'Aborts a load test') << {
	SupervisorUtils.executeCommandLine("./console", commonConfig.consoleDir,
			[
				'-testplan',
				"testplans/$testplan",
				'-abort'
			])
}

task runPerfAlyzer(dependsOn: loadTestConfiguration, description: 'Run perfAlyzer to create the report') << {
	String absoluteResultsDir = testResultsDir.getAbsolutePath()

	SupervisorUtils.executeCommandLine("./perfalyzer", commonConfig.perfAlyzerDir,
			[
				'-i',
				"${absoluteResultsDir}",
				'-o',
				"output${tenant ? '/'+ tenant : ''}"
			])
}

task startDaemons(dependsOn: loadTestConfiguration, description: 'Starts daemons') << { supervisorTasks.startDaemons() }

task stopDaemons(dependsOn: loadTestConfiguration, description: 'Stops daemons') << { supervisorTasks.stopDaemons() }

task restartDaemons(dependsOn: loadTestConfiguration, description: 'Restart daemons') << {
	supervisorTasks.stopDaemons()
	sleep 5000L
	supervisorTasks.startDaemons()
}

task startPerfmons(dependsOn: init, description: 'Starts perfmons') <<{ supervisorTasks.startPerfmons() }

task stopPerfmons(dependsOn: init, description: 'Stops perfmons') << { supervisorTasks.stopPerfmons() }

task cleanupFiles(dependsOn: init, description : 'Perform all clean-up tasks.') << {
	cleanupConsoleFiles()
	supervisorTasks.cleanupDaemonFiles()
	supervisorTasks.cleanupClientFiles()
	supervisorTasks.cleanupMeasuringFiles()
	supervisorTasks.cleanupPerfmonFiles()
	supervisorTasks.cleanupConfiguredFiles()
}

task startServers(dependsOn: init, description: 'Starts up configured servers') << { supervisorTasks.execStartupCommands() }

task stopServers(dependsOn: init, description: 'Shuts down configured servers') << { supervisorTasks.execShutdownCommands() }

task restartServers(dependsOn: init, description: 'Restarts configured servers') << {
	supervisorTasks.execShutdownCommands()
	supervisorTasks.execStartupCommands()
}

task zipFiles(description: 'Zips all result files via SSH') << {
	zipConsoleFiles()
	supervisorTasks.zipDaemonLogs()
	supervisorTasks.zipClientLogs()
	supervisorTasks.zipMeasuringLogs()
	supervisorTasks.zipPerfmonLogs()
	supervisorTasks.zipConfiguredFiles()
}

task downloadFiles(description: 'Downloads all result files via SCP.') << {
	supervisorTasks.downloadDaemonLogs()
	supervisorTasks.downloadClientLogs()
	supervisorTasks.downloadMeasuringLogs()
	supervisorTasks.downloadPerfmonLogs()
	supervisorTasks.downloadConfiguredFiles()
}

/**
 * Prompts the user to select a testplan and returns the selected one.
 */
def String promptForTestplan() {
	int i = 1
	Map<Integer, String> testplans = [:]
	new File(commonConfig.consoleDir, "testplans/${tenant ? tenant : ''}").eachFileMatch(~/(?!testjars).*\.xml/, { testplans[i++] = it.name })

	String lineSep = System.properties.'line.separator'
	String prompt = "${lineSep}Please select the testplan you'd like to use:$lineSep"
	prompt += testplans.collect{ key, value -> "${StringUtils.leftPad(String.valueOf(key), 3)}) $value" }.join(lineSep) + lineSep

	// display all configured testplans prefixed by running numbers,
	// which the user needs to enter in order to select the desired testplan
	while (true) {
		def testplanIndex = System.console().readLine(prompt)
		try {
			int index = Integer.parseInt(testplanIndex)
			if (index <= 0 || index > testplans.size()) {
				continue
			}
		} catch (NumberFormatException ex) {
			continue
		}
		return "${tenant ? tenant + '/' : ''}${testplans[testplanIndex as int]}"
	}
}

/**
 * Reads daemon information from the testplan.
 */
def Map<String, List<Integer>> readDaemonsFromTestplan(String testplanPath) {
	Map<String, List<Integer>> daemonsMap = [:]

	def config = new XmlSlurper().parse(testplanPath)
	config.daemons.daemon.each {
		String host = it.@host.text()
		Integer port = Integer.valueOf(it.@port.text())
		List<String> ports = daemonsMap[host]
		if (!ports) {
			// add new list to the map
			daemonsMap[host] = [port]
		} else {
			// append port to existing list for the current host
			ports << port
		}
	}
	return daemonsMap
}

def zipConsoleFiles() {
	File testplanFile = new File("${commonConfig.consoleDir}/testplans/${testplan}")
	def config = new XmlSlurper().parse(testplanFile)
	String eventsFile = config.testplans.testplan.loadProfileConfig

	task(zipConsoleFiles, type: Zip) {
		from(commonConfig.consoleDir) {
			include '*.log'
			include 'ltStatus.txt'
			include 'ltThreads.txt'
			include 'perfload.meta.utf8.props'
		}
		from(testplanFile.parent) { include testplanFile.name }
		from("${testplanFile.parent}/loadprofiles") { include eventsFile }
		archiveName = "console-logs.zip"
		destinationDir = file("${testResultsDir}/console")
	}.execute()
}

def cleanupConsoleFiles() {
	ant.delete {
		fileset(dir: "${commonConfig.consoleDir}") {
			include(name: '*.log')
			include(name: 'ltStatus.txt')
			include(name: 'ltThreads.txt')
			include(name: 'perfload.meta.utf8.props')
		}
	}
}
